buildscript {
    repositories {
        mavenLocal()
        // https://repo.maven.apache.org/maven2
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.8'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'idea' // optional (to generate IntelliJ IDEA project files)
    id 'eclipse' // optional (to generate Eclipse project files)
}

allprojects {
    group = 'io.github.imsejin.study.springframework'
    version = '0.1.0'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    // apply plugin: 'idea'
    // apply plugin: 'eclipse'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    dependencies {
//        implementation rootProject // All sub projects will import root project.
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
        implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'

        compileOnly group: 'org.projectlombok', name: 'lombok'
        annotationProcessor group: 'org.projectlombok', name: 'lombok'
        testCompileOnly group: 'org.projectlombok', name: 'lombok'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

        /*
        spring-boot v2.3 이후로 "spring-boot-starter-web", "spring-boot-starter-webflux"가
        "spring-boot-starter-validation"를 포함하지 않는다.
        (https://www.youtube.com/watch?v=cP8TwMV4LjE&t=70s)
         */
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

        testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
